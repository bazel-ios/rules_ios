load("@io_bazel_stardoc//stardoc:stardoc.bzl", "stardoc")
load("@bazel_skylib//:bzl_library.bzl", "bzl_library")

package(default_visibility = ["//visibility:private"])

_RULES = [
    "app",
    "apple_patched",
    "framework",
    "hmap",
    "library",
    "precompiled_apple_resource_bundle",
    "substitute_build_settings",
    "test",
    "transition_support",
]

_REPOSITORY_RULES = [
    "framework_builder",
]

bzl_library(
    name = "rules_cc",
    srcs = [
        "@rules_cc//cc:srcs",
    ],
    tags = ["manual"],
    visibility = ["//visibility:private"],
)

bzl_library(
    name = "data",
    srcs = [
        "//data:xcspecs.bzl",
    ],
    tags = ["manual"],
    visibility = ["//visibility:private"],
)

[
    stardoc(
        name = "%s-docs" % name,
        out = "%s_doc.md" % name,
        input = "//rules:%s.bzl" % name,
        tags = ["manual"],
        visibility = ["//:__pkg__"],
        deps = [
            ":data",
            ":rules_cc",
            "@build_bazel_rules_apple//apple",
        ],
    )
    for name in _RULES
]

[
    stardoc(
        name = "%s-docs" % name,
        out = "%s_doc.md" % name,
        input = "//repository_rules:%s.bzl" % name,
        tags = ["manual"],
        visibility = ["//:__pkg__"],
        deps = [
            ":data",
            "@build_bazel_rules_apple//apple",
        ],
    )
    for name in _REPOSITORY_RULES
]

filegroup(
    name = "_all_docs",
    srcs = ["%s-docs" % name for name in (_RULES + _REPOSITORY_RULES)],
    tags = ["manual"],
)

genrule(
    name = "docs",
    srcs = [
        ":_all_docs",
    ],
    outs = [
        "generate_docs.sh",
    ],
    cmd = """
echo "#!/bin/bash" > $(OUTS)
echo "set -eu" > $(OUTS)
echo 'function copy() { 
    dest="$${BUILD_WORKSPACE_DIRECTORY}/docs/$$(basename $$1)"
    cp "$$1" "$$dest"
    chmod +w "$$dest"
}' >> $(OUTS)
for s in $(SRCS); do
    echo "copy '$$s'" >> $(OUTS)
done
    """,
    executable = 1,
    tags = ["manual"],
)
